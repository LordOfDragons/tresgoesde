namespace TresGoesDE.Elements

pin Dragengine.Scenery
pin Dragengine.Preloading
pin Dragengine.Utils


/**
 * Moeavable geometry. This is CInstance with Moveable=1.
 */
class MoveableGeometryClass extends BehaviorElementClass
	protected var ECBehaviorComponent pComponent
	protected var ECBehaviorCollider pCollider
	protected var ECBFreezable pFreezable
	protected var ECBehaviorAttachToParent pAttachToParent
	protected var ECBehaviorAnchorElement pAnchorElement
	
	
	public func new() this( "MoveableGeometry" )
	end
	
	public func new( String className ) super( className )
		pComponent = ECBehaviorComponent.new( this, null )
		
		pCollider = ECBehaviorCollider.new( this, pComponent )
		
		pCollider.getCollider().getPhysicsType().setEnumeration( CollisionResponse.dynamic )
		
		var LayerMask lmCategory = LayerMask.newWith( BaseGameApp.CollisionFilterBit.dynamic )
		
		var LayerMask lmFilter = LayerMask.new()
		lmFilter.setBit( BaseGameApp.CollisionFilterBit.dynamic )
		lmFilter.setBit( BaseGameApp.CollisionFilterBit.geometry )
		lmFilter.setBit( BaseGameApp.CollisionFilterBit.actor )
		lmFilter.setBit( BaseGameApp.CollisionFilterBit.particle )
		
		pCollider.getCollider().setCollisionFilter( CollisionFilter.new( lmCategory, lmFilter ) )
		
		pFreezable = ECBFreezable.new( this, pCollider )
		pAttachToParent = ECBehaviorAttachToParent.new( this, pCollider, null )
		pAnchorElement = ECBehaviorAnchorElement.new( this )
	end
	
	public func ElementClass duplicateNamed( String classname )
		return MoveableGeometryClass.new( classname )
	end
end
